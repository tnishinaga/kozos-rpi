OUTPUT_ARCH(arm)
ENTRY("_start")

MEMORY
{
	/* o: ORIGIN, l:LENGTH */
	ramall(rwx)	: o = 0x00000000, l = 0x1c000000 /* 512-64MB */
	softvec(rw)	: o = 0x00000000, l = 0x00000040 /* top of RAM */
	ram(rwx)	: o = 0x00008000, l = 0x1bff8000 /* entry point(0x8000) - end(0x1c000000) */

	userstack(rw)	: o = 0x19000000, l = 0x00000000 /* fiq - 16MB */
	/* fiqstack(rw)	: o = 0x1a000000, l = 0x00000000 */ /* irq - 16MB */
	irqstack(rw)	: o = 0x1b000000, l = 0x00000000 /* svc - 16MB */
	svcstack(rw)	: o = 0x1c000000, l = 0x00000000 /* end of RAM */
}

SECTIONS
{
	.softvec : {
		_softvec = .;
	} > softvec

	.text : {
		_text_start = . ;
		*(.text)
		_etext = . ;
	} > ram

	.rodata : {
		_rodata_start = . ;
		*(.strings)
		*(.rodata)
		*(.rodata.*)
		_erodata = . ;
	} > ram

	.data : {
		_data_start = . ;
		*(.data)
		_edata = . ;
	} > ram

	.bss : {
		_bss_start = . ;
		*(.bss)
		*(COMMON)
		_ebss = . ;
	} > ram

	. = ALIGN(4);
	_end = . ;

	.freearea : {
		_freearea = .;
	} > ram

	.userstack : {
		_userstack = .;
	} > userstack

	.irqstack : {
		_irqstack = .;
	} > irqstack

	.svcstack : {
		_svcstack = .;
	} > svcstack
}
